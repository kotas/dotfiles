#!/usr/bin/perl

use strict;
use warnings;

my $pattern = shift @ARGV;
die "Usage: $0 's/old/new/' FILE..\n" unless $pattern and @ARGV;

$pattern = "s$pattern" unless $pattern =~ /^s/;
print "Pattern: $pattern\n";

my @files = ();
for my $arg (@ARGV) {
	die "File not found: $arg\n" unless -f $arg || -d $arg;

	my $renamed = $arg;
	my $replaced;
	eval "\$replaced = \$renamed =~ $pattern";
	if ($@) {
		die "Error: $@\n";
	}
	if ($replaced) {
		push @files, +{ old => $arg, new => $renamed };
		print "$arg => $renamed\n";
	} else {
		print "Skipped $arg\n";
	}
}

print "\nAre you sure? (y/N): ";
my $got = <STDIN>;
exit if ($got !~ /^y/i);

for my $file (@files) {
	my $success = rename $file->{old}, $file->{new};
	if ($success) {
		print "Renamed: $file->{old} => $file->{new}\n";
	} else {
		print "FAILED: $file->{old} => $file->{new}\n";
	}
}


